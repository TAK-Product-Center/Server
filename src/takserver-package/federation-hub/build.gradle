buildscript {
    dependencies {
        classpath 'com.netflix.nebula:gradle-ospackage-plugin:' + gradle_ospackage_version
    }
}

apply plugin: 'nebula.ospackage'
apply from: "../utils/utils.gradle"

// Copy README.
task copyFedHubReadme(type: Copy) {
    from getRootProject().file('docs')
    include 'README_fedhub.md'
    into "$buildDir/artifacts/docs"
    rename 'README_fedhub.md', 'README.md'
}

// Copy Docker README.
task copyFedHubDockerReadme(type: Copy) {
    from getRootProject().file('docs')
    include 'README_fedhub_docker.md'
    into "$buildDir/artifacts/docs"
}

// Copy configs.
task copyFedHubConfigs(type: Copy) {
    from getRootProject().subprojects.collect { "${it.projectDir}/src/main/resources" }
    include 'federation-hub-*.yml', 'logback-*.xml'
    into "$buildDir/artifacts/configs"
}

// Copy executables.
task copyFedHubJars(type: Copy) {
    dependsOn(':federation-hub-broker:build')
    dependsOn(':federation-hub-policy:build')
    dependsOn(':federation-hub-ui:bootWar')
    dependsOn(':federation-hub-ui:bootJar')
    dependsOn(':federation-hub-ui:shadowJar')

    from getRootProject().subprojects.collect { "${it.buildDir}/libs" }
    into "$buildDir/artifacts/jars"
    include 'federation-hub-*-' + version + '.jar', 'federation-hub-ui-' + version + '.war'
    rename 'federation-hub-broker-*' + version + '.jar', 'federation-hub-broker.jar'
    rename 'federation-hub-policy-*' + version + '.jar', 'federation-hub-policy.jar'
    rename 'federation-hub-ui-*' + version + '.war', 'federation-hub-ui.war'
    rename 'federation-hub-manager-*' + version + '.jar', 'federation-hub-manager.jar'
}

// Copy scripts.
task copyFedHubScripts(type: Copy) {
    from getRootProject().subprojects.collect { "${it.projectDir}/scripts" }
    include 'federation-hub*'
    include 'db/'
    into "$buildDir/artifacts/scripts"
}

// Copy certificate scripts.
task copyCertScripts(type: Copy) {
    from project(':takserver-core').file('scripts/certs')
    into "$buildDir/artifacts/certs"
}

task copySELinuxPolicy(type: Copy) {
    from project(':takserver-core').file('scripts/utils')
    include 'takserver-policy.te'
    into "$buildDir/artifacts"
}

task copySELinuxScript(type: Copy) {
    from project(':takserver-core').file('scripts')
    include 'apply-selinux.sh'
    into "$buildDir/artifacts"
}

// RPM pre-install script.
def preinstall_script_federation_hub = '''\

mkdir -p /opt/tak/federation-hub

# Stop the federation hub if it is still running.
set +e
pkill -9 -f federation-hub-broker.jar
pkill -9 -f federation-hub-policy.jar
pkill -9 -f federation-hub-ui.war
set -e

if service --status-all | grep -Fq 'federation-hub'; then
  systemctl stop federation-hub
fi

# Add tak user if it doesn't already exist.
set +e
uid=`id -u tak 2>/dev/null`
set -e
if [ -z "$uid" ] ; then
    # User id 1491 is arbitrary
    echo "Adding 'tak' user"
    useradd tak -r -u 1491 --no-create-home -d /opt/tak
fi

# try to backup configs
if [ -d "/opt/tak/federation-hub/configs" ] ; then
    echo "Backing up /opt/tak/federation-hub/configs to /opt/tak/federation-hub/backup-configs"
    mkdir -p /opt/tak/federation-hub/backup-configs
    cp -fr /opt/tak/federation-hub/configs/* /opt/tak/federation-hub/backup-configs/
fi

'''

// RPM post-install script.
def postinstall_script_federation_hub = '''\
chown tak:tak /opt/tak

# try to backup configs
if [ ! -d "/opt/tak/federation-hub/configs" ] ; then
    mkdir -p /opt/tak/federation-hub/configs
    cp -fR /opt/tak/federation-hub/default-configs/* /opt/tak/federation-hub/configs/
fi

chown -fR tak:tak /opt/tak/federation-hub/configs

# Set permissions on selinux configuration files and scripts.
chmod 544 /opt/tak/federation-hub/*.sh

# Set permissions on configuration files and scripts.
chmod 644 /opt/tak/federation-hub/configs/*.xml 
chmod 644 /opt/tak/federation-hub/configs/*.yml

chmod 544 /opt/tak/federation-hub/scripts/*.sh
chmod u+w /opt/tak/federation-hub/scripts/*.sh

chmod 544 /opt/tak/federation-hub/scripts/db/*.sh
chmod u+w /opt/tak/federation-hub/scripts/db/*.sh

# Set permissions on certificate scripts.
chmod 500 /opt/tak/federation-hub/certs/*.sh
chmod 600 /opt/tak/federation-hub/certs/cert-metadata.sh

# Put init scripts into place.
chown root:root /opt/tak/federation-hub/scripts/federation-hub
chown root:root /opt/tak/federation-hub/scripts/federation-hub-broker
chown root:root /opt/tak/federation-hub/scripts/federation-hub-policy
chown root:root /opt/tak/federation-hub/scripts/federation-hub-ui
chmod 755 /opt/tak/federation-hub/scripts/federation-hub
chmod 755 /opt/tak/federation-hub/scripts/federation-hub-broker
chmod 755 /opt/tak/federation-hub/scripts/federation-hub-policy
chmod 755 /opt/tak/federation-hub/scripts/federation-hub-ui
cp /opt/tak/federation-hub/scripts/federation-hub /etc/init.d
cp /opt/tak/federation-hub/scripts/federation-hub-broker /etc/init.d
cp /opt/tak/federation-hub/scripts/federation-hub-policy /etc/init.d
cp /opt/tak/federation-hub/scripts/federation-hub-ui /etc/init.d


# Set up logging directory.
mkdir -p /opt/tak/federation-hub/logs
chown tak:tak /opt/tak/federation-hub/logs
chmod 755 /opt/tak/federation-hub/logs

cat <<- "EOF"

TAK SERVER FEDERATION HUB SOFTWARE LICENSE AGREEMENT

Distribution Statement A: Approved for public release; distribution is unlimited.

----

For secure operation, the Federation Hub requires a keystore and truststore (for X.509 certificates).
Follow the instructions in Appendix B of the configuration guide to create these certificates.

Enable adminstrative access for a user to the configuration interface:

sudo java -jar /opt/tak/federation-hub/jars/federation-hub-manager.jar path/to/cert.pem

Using Firefox or Chrome on this computer, browse to this address to access the configuration interface:
https://localhost:9100/index.html

Log-in with the administrative certificate enabled above.

Follow the instructions in the Installation section of the configuration guide to complete the setup process.

EOF
'''

// RPM post-uninstall script.
def postuninstall_script_federation_hub = '''\
case "$1" in
 0|remove)
        # Delete user if TAK server is not running.
        if [ `service --status-all | grep -Fc 'takserver-messaging'` -eq 0 ] && \
           [ `service --status-all | grep -Fc 'takserver-api'` -eq 0 ] && \
           [ `service --status-all | grep -Fc 'takserver-config'` -eq 0 ]
        then
            echo "Deleting tak user"
            userdel tak
        fi
        systemctl stop federation-hub >/dev/null 2>&1
        systemctl disable federation-hub >/dev/null 2>&1
        rm /etc/init.d/federation-hub-broker
        rm /etc/init.d/federation-hub-policy
        rm /etc/init.d/federation-hub-ui
        rm /etc/init.d/federation-hub
        ;;
 1|upgrade)
        # This is an upgrade, do nothing.
        :
        ;;
esac
'''

ospackage {
    packageName = 'takserver-fed-hub'
    version = takversion
    release = takreleaserpm
    os = 'LINUX'
    user = 'tak'
    permissionGroup = 'tak'
    packageGroup = 'Applications/Internet'
    packageDescription = '''\
The Federation Hub is an optional companion to the Team Awareness Kit (TAK) server.
It allows for the federation of TAK data across the administrative and trust
boundaries of different networks participating in a TAK deployment. It can be run
with or without a co-located TAK server.
'''
    license = '(c)2013-2021 Raytheon BBN. Licensed to US Government with unlimited rights.'
    url = 'https://tak.gov'
    summary = 'Team Awareness Kit (TAK) Federation Hub'

    preInstall preinstall_script_federation_hub
    postInstall postinstall_script_federation_hub
    postUninstall postuninstall_script_federation_hub

    from ("$buildDir/artifacts") {
        exclude 'configs/'
        into '/opt/tak/federation-hub'
        fileMode = 444
        dirMode = 755
    }

    from ("$buildDir/artifacts/configs") {
        into '/opt/tak/federation-hub/default-configs'
        fileMode = 444
        dirMode = 755
    }
}

task prePackage(type: Copy) { task ->
    with copyLicenseSpec()
    destinationDir file("$buildDir/takArtifacts")
}

prePackage.dependsOn copyFedHubReadme
prePackage.dependsOn copyFedHubDockerReadme
prePackage.dependsOn copyFedHubConfigs
prePackage.dependsOn copyFedHubJars
prePackage.dependsOn copyFedHubScripts
prePackage.dependsOn copyCertScripts
prePackage.dependsOn copySELinuxPolicy
prePackage.dependsOn copySELinuxScript

buildRpm.dependsOn prePackage

buildDeb.dependsOn prePackage

buildRpm {
    requires('java-17-openjdk-devel')
}

buildDeb {
  requires('openjdk-17-jdk')
}

def copyFedhubResourcesDef() { return copySpec {
    from 'build/artifacts'
}}
def copyFedhubDockerDef() { return copySpec {
    from project(':federation-common').file('docker/')
}}
def copyFedhubDockerScriptDef() { return copySpec {
    from project(':federation-common').file('scripts/configureInDocker.sh')
}}
def copyCertsSetupDef() { return copySpec {
    from project(':takserver-core').file('scripts/certs')
}}
def copyDocsToTopLevelDef() { return copySpec {
    from 'build/artifacts/docs'
}}

task createFedhubDockerLogDir() {
    doLast{
        mkdir "build/artifacts/logs"
    }
}

def copyFedhubResources = copyFedhubResourcesDef()
def copyFedhubDocker = copyFedhubDockerDef()
def copyFedhubDockerScript = copyFedhubDockerScriptDef()
def copyCertsSetup = copyCertsSetupDef()
def copyDocsToTopLevel = copyDocsToTopLevelDef()

task constructFedhubDockerZip(type: Zip) { Task task ->
    dependsOn createFedhubDockerLogDir
    dependsOn buildRpm
   
    archiveName 'takserver-fedhub-docker-' + version + '.zip'
    duplicatesStrategy 'fail'
    destinationDir(file("$buildDir/distributions"))
    into('takserver-fedhub-docker-' + version)

    with copyVersionFileSpec(task).into('tak')
    with copyFedhubResources.exclude("configs/federation-hub-broker.yml", "configs/federation-hub-broker-docker.yml")
            .into('tak/federation-hub')
    with copyFedhubDockerScript.into('tak/federation-hub/scripts')
    with copyFedhubDocker.into('docker')
    with copyCertsSetup.into('tak/certs')
    with copyDocsToTopLevel.into('./')
    
    from(project(':federation-hub-broker').file('src/main/resources')) {
    	include('federation-hub-broker-docker.yml')
		rename('federation-hub-broker-docker.yml', 'federation-hub-broker.yml')
		into('tak/federation-hub/configs')
	}

}
